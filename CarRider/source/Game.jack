/**
Main game controller;
*/
class Game {

    static Game instance; // the singelton, a game instance     
    field Car car;        // Car instance
    field Block block;    // Block 1
    field Block block2;   // Block 2
    field boolean exit;   // true when the game is over
    
    
    /** Create an new game */
    constructor Game new() {
	    do Screen.clearScreen();
        do Screen.drawLine(150, 0, 150, 255);
        do Screen.drawLine(218, 0, 218, 255);
        let car = Car.new();
        let block = Block.new(0);
        let block2 = Block.new(1);
	    do Output.moveCursor(22,0);
	    do Output.printString("Score: 0");
        
        let exit = false;

        return this;
    }

    /** Creates an instance of game, and stores it. */
    function void newInstance() {
        let instance = Game.new();
        return;
    }
    
    /** Returns the single instance of this game. */
    function Game getInstance() {
        return instance;
    }
    
    /** Deallocates the object's memory. */
    method void dispose() {
        do car.dispose();
	    do block.dispose();
        do block2.dispose();
        do Memory.deAlloc(this);
        return;
    }

    
    /** Starts the game, and lets inputs from the user that control
     *  the bat's movement direction. */
    method void run() {
        var char key;
        while (~exit) {
            // waits for a key to be pressed.
            while ((key = 0) & (~exit)) {
                let key = move();
            }

            if (key = 130) {    
                do car.moveRight();
            }
	        else {
	            if (key = 132) { 
                    do car.moveLeft();
                }
		        else {
                    if (key = 140) { let exit = true; }
		        }
            }

            // Waits for the key to be released.
            while ((~(key = 0)) & (~exit)) {
                let key = move();
            }
        }

	    if (exit) {
            do Output.moveCursor(10,27);
	        do Output.printString("Game Over");
	    }
            
        return;
    }
    
    /** Do game move to next screen */
    method char move() {
        var char key;
        
        let key = Keyboard.keyPressed();
                
        do block.moveDown();
        do block2.moveDown();
        
        let exit = block.isHit() | block2.isHit();
        
        do Output.moveCursor(22,7);
        do Output.printInt(block.pass());

        do Sys.wait(50);
        
        return key;
    }


}